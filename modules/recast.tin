#action {^setcast_{New v|V}ariable {changed|set} to '%3'.$}
{
#variable {recast} {%3}
}
{1}

#ACTION {^You barely remember anything about that spell, but attempt to cast it anyway.$}
{
	#variable {recast} nocast
}
{2}

#ACTION {^You fail to {channel|cast} the spel%1.$}
{
#if {"$autorecast" == "true" && "$recast" != "nocast"} {$recast}
}
{2}

#ACTION {^You can't seem to wake up!$}
{
	#variable {recast} {nocast}
}
{2}

#ACTION {^You are asleep... ZZZZZZZZ *snork* ZZZZZ...}
{
	getup;
	$recast
}
{9}

#ACTION {^silent_Missing '%1'.$}
{
	#if {"$autorecast" == "true" && $currentMana > $maxMana / 8 && !&{inBattle} && "$busy" != "true"}
	{
		c %1
	}
}
{9}

#ALIAS {^{cc|cca|ccas|ccast|channelc|channelca|channelcas|channelcast} %1}
{
	variable cast %0 | prefix setcast_;
	\channelcast %1
}
{1}

#ALIAS {{c|ca|cas|cast} %1}
{
	variable cast %0 | prefix setcast_;
	#if {&{position}}
	{
		getup;
		#send {cast %1};
		$position
	};
	#else
	{
		#send {cast %1}
	}
}
{1}

#ALIAS {^Add_Cast %1}
{
	#if {!&{combat}}
	{
		#variable {combat} {c %1}
	};
	#else
	{
		#variable {combat} {$combat c %1}
	}
}
{9}

#ALIAS {autorecast}
{
	#variable {autorecast} {true};
	#showme {Some spells will now automatically recast when they drop}
}
{9}

#ALIAS {noautorecast}
{
	#unvariable {autorecast};
	#showme {Spells will no longer automatically recast.}
}
{9}
#gag {setcast_%*}
#variable {recast} {}
